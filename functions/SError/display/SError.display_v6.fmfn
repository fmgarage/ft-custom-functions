/*	SError.display_v6( _plugin )
	@author Nils Waldherr <nils.waldherr@fmgarage.com>
	@version 1.3.0-100 - 09.10.2007 - Nils Waldherr
	         2.0.0-100 - 05.08.2009 - NW
             2.1.1-100 - 15.09.2010 - NW: Kargas DialogPlugin
             2.1.2-100 - 22.09.2010 - NW: max height
             2.5.0-100 - 01.02.2011 - NW: re-implementing _class
             2.5.1-100 - 20.06.2011 - NW: bug on Windows
             3.0.0-100 - 24.03.2012 - NW: renamed, 
             3.1.0-100 - 08.08.2012 - NW: no more implicit catch()
             3.2.0-100 - 31.10.2012 - NW: version mode
             3.2.1-101 - 27.11.2013 - NW: better output of class & type
             3.2.2-102 - 28.12.2013 - NW: fixed position
             4.0.2-103 - 15.02.2014 - NW: using BaseElements plugin, moved to f1, ML
             4.1.0-104 - 16.04.2014 - NW: level as title
             4.1.1-105 - 02.05.2014 - NW: version sets function name
             4.2.2-108 - 11.05.2014 - NW: save to $$_Exception
             4.2.0-106 - 06.05.2014 - NW: localise vars
             4.2.1-107 - 09.05.2014 - NW: return code on empty message
             5.0.0-109 - 25.05.2014 - NW: array format
             5.0.2-111 - 26.05.2014 - NW: to string in global $$_Exception.to_string
             5.0.3-112 - 26.08.2014 - AB: text format for windows used in BE_SetClipboardText, prepare text for windows linesep., require BE 2.3
             5.0.4-113 - 20.04.2015 - AB: require BE 2.3
             5.1.0-114 - 20.12.2015 - NW: uses _Script.debug()
             5.1.1-116 - 17.01.2016 - NW: fix BE version precondition
             5.1.2-117 - 25.03.2017 - NW: revision
             5.1.3-118 - 08.09.2017 - NW: script_id
             5.1.4-119 - 24.09.2017 - NW: string from throw()
             5.1.5-120 - 28.03.2019 – NW: .string fix
             6.0.0-121 - 06.06.2021 – NW: rewrite, json, moved to SError
             6.0.1-122 - 20.08.2021 – UZ: renamed BE function with BE version 4.0.2
             6.0.2-123 - 25.08.2021 – NW: use MBS alternaively, _plugin
             6.0.3-125 - 12.10.2021 – NW: fix cast version, $SError
             6.1.0-126 - 04.11.2021 – NW: french, spanish
              
	@package f1.system.error
	@param string _plugin = 'auto' -- mbs, be
	@returns null
	
	@uses function SError.catch() 4.0
	@uses function SError.throw() 5.10
	@uses function _Script.debug() 2.0
	@uses plugin BaseElements 3.1
*/

Case(

	/*	return version
	    ----------------------------------------------------------------------------------
	*/
	_plugin = "--version" or $_Db.inVersionMode; 6000003;



	/*	preconditions
	    ----------------------------------------------------------------------------------
	*/

	// no exception present
	IsEmpty( $SError ); "" ;
	

	// check available plugins
	Case(
		IsEmpty( $$SError.display.plugin );
		Let(
			[
				mbs = Case( 
					not IsEmpty( $test_mbs_version ); 
					$test_mbs_version; 
					MBS( "VersionAutoupdate")+0
				); 
				be = Case( 
					not IsEmpty( $test_be_version ); 
					$test_be_version; 
					BE_VersionAutoUpdate+0
				);
				$$SError.display.plugin = Case(

					// from parameter
					not IsEmpty( FilterValues( _plugin; "mbs¶be" )); _plugin;
					
					// auto
					mbs > 7020000; "mbs"; 
					be >= 3010000; "be";
					"none"
				)
			];
			""
		)
	) or $$SError.display.plugin = "none"; 
	"ERROR: BaseElements 3.1 or MBS Plugin 7.2 required";



	/*	main
	    ----------------------------------------------------------------------------------
	*/
	Let(
		[
			// make exception global
			$$SError = $SError;
			$$SError.string = $SError.string;
			
			// compose dialog
			level_names = Case(
				Get ( ApplicationLanguage ) = "German";
				"Fehler¶Fehler¶Warnung¶Info¶Info";
				"Error¶Error¶Warning¶Info¶Info"
			);
			type = JSONGetElement( $SError; "SError.type" );
			class = JSONGetElement( $SError; "SError.class" );
			code = JSONGetElement( $SError; "SError.code" );
			title = Case(
				class = "F1"; code;
				
				// de
				Get ( ApplicationLanguage ) = "German"; "Fehler";

				// fr
				Get ( ApplicationLanguage ) = "French"; "Erreur";
				
				// default: en, es
				"ERROR"
			);
			text = JSONGetElement( $SError; "SError.message" );
			text = Case( 
				// message
				not IsEmpty( text ); text;

				// generic de
				Get ( ApplicationLanguage ) = "German";
				"Ein Fehler ist aufgetreten!";

				// generic fr
				Get ( ApplicationLanguage ) = "French";
				"Une erreur s'est produite!";

				// generic es
				Get ( ApplicationLanguage ) = "Spanish";
				"Se ha producido un error!";

				// generic default en
				"An Error has occurred"
			)
			& Case( not IsEmpty( code ); " (" & code & ")");
			copy_button = Case( 
			
				// de
				Get( ApplicationLanguage ) = "German"; 
				"Kopieren"; 
				
				// fr
				Get( ApplicationLanguage ) = "French"; 
				"Copier"; 
				
				// es
				Get( ApplicationLanguage ) = "Spanish"; 
				"Copiar"; 
				
				// default: en
				"Copy"
			)
		];		
		Case(

			// MBS
			$$SError.display.plugin = "mbs";
			Let(
				[
					r = MBS( "Dialog.Reset" );
					r = MBS( "Dialog.SetMode"; "Error" );
					r = MBS( "Dialog.SetDefaultButton"; "OK" );
					r = MBS( "Dialog.SetAlternateButton"; copy_button );
//					r = MBS( "Dialog.SetWindowTitle"; title );
					r = MBS( "Dialog.SetMessage"; title );
					r = MBS( "Dialog.SetInformativeText"; text );
					r = MBS( "Dialog.Run" );
					button = MBS( "Dialog.GetButtonPressed" );
					x=1
				];
				Case( 
					button = 2; 
					MBS( "Clipboard.SetText"; $SError.string ) 
				)
			);
			
			// BaseElements
			$$SError.display.plugin = "be";
			Let(
				[
					button = BE_DialogDisplay( title; text; "OK"; copy_button );
			
					// set clipboard
					text = Substitute( $SError.string ; "¶" ; Char(13) & Char(10)  );
					format = Case(
						// Windows
						Get ( SystemPlatform ) = "-2";
						"CF_UNICODETEXT";
						// Mac
						Get ( SystemPlatform ) = "1";
						"public.utf8-plain-text"
					);

					// set clipboard
					r = Case( button = 2; BE_ClipboardSetText( text ; format ))
				];
				""
			)
		)
	)
)
